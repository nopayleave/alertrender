//@version=5
//  ________________                                        
//  ___  __ )_____(_)___________________ ____  ________ ___ 
//  __  __  |____  /_  __ \_  ___/_  __ `/  / / /_  __ `__ \
//  _  /_/ /____  / / /_/ /  /   _  /_/ // /_/ /_  / / / / /
//  /_____/ ___  /  \____//_/    _\__, / \__,_/ /_/ /_/ /_/ 
//          /___/                /____/                     

indicator( "Bjorgum TSI", "BJ TSI", precision = 3, explicit_plot_zorder = true)



//#region ———————————————————— Constants and inputs


string SI1 = "Fast"
string SI2 = "Slow"
string TI1 = "Classic Bj"
string TI2 = "Nightlife"
string TI3 = "Beach Day"

string GRP1             = "TSI Control"
string speedInput       = input.string(SI1, "Select a Speed", group = GRP1, options = [SI1, SI2])
string themeInput       = input.string(TI1, "Select a Color", group = GRP1, options = [TI1, TI2, TI3])

string GRP2             = "Overbought/Oversold Lines"
int    oBotInput        = input.int(30,     "Ob Value",         group = GRP2)
int    oSoldInput       = input.int(-30,    "Os Value",         group = GRP2)
int    noShortInput     = input.int(60,     "No Short Value",   group = GRP2)
int    onLongInput      = input.int(-40,    "On Long Value",    group = GRP2)
bool   showLinesInput   = input.bool(false, "Show Ob/Os Lines", group = GRP2)
bool   showNoShortInput = input.bool(true, "Show No Short Line", group = GRP2)
bool   showOnLongInput  = input.bool(true, "Show On Long Line", group = GRP2)
bool   showHiLoInput    = input.bool(true, "Show Ob/Os Cross", group = GRP2)
bool   showMidInput     = input.bool(true, "Show Zero Line",   group = GRP2)
bool   showCurlInput    = input.bool(true, "Ob/Os Curl Color", group = GRP2) 
bool   blankBarsInput   = input.bool(false, "Show White Bars",  group = GRP2)

string GRP3             = "TSI Value Line"
color  tsiBullInput     = input.color(color.new(#09ff00, 0),  "", group = GRP3, inline = "1")
color  tsiMidAboveInput = input.color(color.new(#09ff00,0),  "", group = GRP3, inline = "1")
color  tsiMidBelowInput = input.color(color.new(#ea2b2b, 0),  "", group = GRP3, inline = "1")
color  tsiBearInput     = input.color(color.new(#ea2b2b,0),  "", group = GRP3, inline = "1") 

string GRP4             = "TSI Signal Line"
color  tslBullInput     = input.color(color.new(#09ff00,0),  "", group = GRP4, inline = "2")
color  tslBearInput     = input.color(color.new(#ea2b2b,0),  "", group = GRP4, inline = "2")

string GRP5             = "Fill Color"
color  fillBullInput    = input.color(color.new(#09ff00,88), "", group = GRP5, inline = "3")
color  fillBearInput    = input.color(color.new(#ea2b2b,88), "", group = GRP5, inline = "3")

string GRP6             = "Ob/Os Line Color"
color  oBotColInput     = input.color(color.new(#ff0000,65), "", group = GRP6, inline = "4")
color  oSoldColInput    = input.color(color.new(#64b5f6,65), "", group = GRP6, inline = "4")
color  noShortColInput  = input.color(color.new(#ffaa00,65), "", group = GRP6, inline = "5")
color  onLongColInput   = input.color(color.new(#00ff00,65), "", group = GRP6, inline = "5")
color  zeroColInput     = input.color(color.new(#b2b5be,65), "", group = GRP6, inline = "4")

string GRP7             = "Bar Color"
color  obBarInput       = input.color(color.new(#ff0000,0),  "", group = GRP7, inline = "5")
color  neBarInput       = input.color(color.new(#ffffff,0),  "", group = GRP7, inline = "5")
color  osBarInput       = input.color(color.new(#17ff00,0),  "", group = GRP7, inline = "5")

string GRP8             = "Strong Bear Area"
bool   showStrongBear   = input.bool(true, "Show Strong Bear Area", group = GRP8)
int    strongBearValue  = input.int(-40, "Strong Bear Value", group = GRP8)
color  strongBearColor  = input.color(color.new(#8b0000, 50), "Background Color", group = GRP8)

string GRP9             = "Bear Area"
bool   showBearArea     = input.bool(true, "Show Bear Area", group = GRP9)
int    bearAreaValue    = input.int(-15, "Bear Area Value", group = GRP9)
color  bearAreaColor    = input.color(color.new(#ff4444, 70), "Background Color", group = GRP9)

string GRP10            = "Bull Area"
bool   showBullArea     = input.bool(true, "Show Bull Area", group = GRP10)
int    bullAreaValue    = input.int(15, "Bull Area Value", group = GRP10)
color  bullAreaColor    = input.color(color.new(#44ff44, 70), "Background Color", group = GRP10)

string GRP11            = "Strong Bull Area"
bool   showStrongBull   = input.bool(true, "Show Strong Bull Area", group = GRP11)
int    strongBullValue  = input.int(40, "Strong Bull Value", group = GRP11)
color  strongBullColor  = input.color(color.new(#00ff00, 50), "Background Color", group = GRP11)

string GRP12            = "TSI Fast Settings"
int    longfLenInput    = input.int(25, "Long Length",   group = GRP12)
int    shortfLenInput   = input.int(5 , "Short Length",  group = GRP12)
int    signalfLenInput  = input.int(14, "Signal Length", group = GRP12)

string GRP13            = "TSI Slow Settings"
int    longsLenInput    = input.int(42, "Long Length",   group = GRP13)
int    shortsLenInput   = input.int(28, "Short Length",  group = GRP13)
int    signalsLenInput  = input.int(29, "Signal Length", group = GRP13)

string GRP14            = "Timeframe Settings"
bool   useCustomTF      = input.bool(false, "Use Custom Timeframe", group = GRP14)
string customTF         = input.timeframe("", "Timeframe", group = GRP14)

string GRP15            = "Color Switch Plot"
bool   showTrendContinueBear = input.bool(true, "Show Trend Continue Bear", group = GRP15, tooltip = "Green to red below signal line")
bool   showTrendContinueBull = input.bool(true, "Show Trend Continue Bull", group = GRP15, tooltip = "Red to green above signal line")
color  trendContinueBearColor = input.color(color.new(#ff8800, 0), "Trend Continue Bear Color", group = GRP15)
color  trendContinueBullColor = input.color(color.new(#00ff88, 0), "Trend Continue Bull Color", group = GRP15)
int    switchSize       = input.int(3, "Switch Plot Size", group = GRP15)
float  switchOffset     = input.float(10.0, "Offset from Value Line", group = GRP15, step = 1.0)

string GRP16            = "Premarket TSI Range"
bool   showPremarketRange = input.bool(true, "Show Premarket Range", group = GRP16)
color  premarketRangeColor = input.color(color.new(#787b86, 90), "Premarket Range Color", group = GRP16)
string premarketTimezone = input.string("America/New_York", "Timezone", group = GRP16, tooltip = "Timezone for premarket detection. Use 'America/New_York' for US stocks, 'UTC' for crypto")
int    premarketStartHour = input.int(4, "Premarket Start Hour", group = GRP16, minval = 0, maxval = 23, tooltip = "Hour when premarket starts (0-23, e.g., 4 for 4:00 AM)")
int    premarketStartMinute = input.int(0, "Premarket Start Minute", group = GRP16, minval = 0, maxval = 59)
int    premarketEndHour = input.int(9, "Premarket End Hour", group = GRP16, minval = 0, maxval = 23, tooltip = "Hour when premarket ends (0-23, e.g., 9 for 9:00 AM)")
int    premarketEndMinute = input.int(30, "Premarket End Minute", group = GRP16, minval = 0, maxval = 59)

string GRP17            = "Webhook Settings"
bool   enableWebhook     = input.bool(true, "Enable Webhook Alerts", group = GRP17)
string webhookFrequency  = input.string("Once Per Bar Close", "Alert Frequency", group = GRP17, options = ["Once Per Bar Close", "Once Per Bar", "All"])
//#endregion



//#region ———————————————————— Functions 


// @function 	    Determines if a given background color corresponds to a light theme based on its brightness level.
// @param bgColor   (color) The background color to check. Optional. Default value is the chart's background color.
// @returns 	    (bool) True if the background color is "light" (has a brightness greater than 0.5), otherwise false.
isLightTheme(color bgColor = chart.bg_color) =>
    float r = color.r(bgColor)
    float g = color.g(bgColor)
    float b = color.b(bgColor)
    float brightness = (r + g + b) / (3 * 255)
    bool  isLight = brightness > 0.5
//#endregion



//#region ———————————————————— Calculations 


bool  tsifast     = speedInput ==  SI1
int   shortvar    = tsifast ? shortfLenInput  : shortsLenInput  
int   longvar     = tsifast ? longfLenInput   : longsLenInput   
int   signalvar   = tsifast ? signalfLenInput : signalsLenInput 

// Determine effective timeframe
string effectiveTF = useCustomTF and customTF != "" ? customTF : timeframe.period

// Define TSI calculation function
calcTSI() =>
    float _tsi = ta.tsi(close, shortvar, longvar) * 100
    float _tsl = ta.ema(_tsi, signalvar)
    [_tsi, _tsl]

// Calculate for chart timeframe
[tsi_chart, tsl_chart] = calcTSI()

// Calculate for custom timeframe (step effect - repeats value until next HTF bar)
[tsi_htf, tsl_htf] = request.security(syminfo.tickerid, effectiveTF, calcTSI())

// Select which data to use
float tsi = useCustomTF and customTF != "" ? tsi_htf : tsi_chart
float tsl = useCustomTF and customTF != "" ? tsl_htf : tsl_chart
//#endregion



//#region ———————————————————— Conditions


bool  tsiIsBull   = tsi >= tsi[1]
bool  tslIsBull   = tsl >= tsl[1]
bool  trendIsBull = tsi >= tsl
float diff        = tsi  - tsl
float barHi       = math.max(0, 0 + diff)
float barLo       = math.min(0, 0 + diff)

bool  curlUp      = tsi > tsi[1] and tsi < tsl
bool  curlDn      = tsi < tsi[1] and tsi > tsl
bool  overBought  = tsi > oBotInput  and tsi < tsi[1] and tsi[1] > tsl[1]
bool  overSold    = tsi < oSoldInput and tsi > tsi[1] and tsi[1] < tsl[1]

bool  crossUp     = ta.crossover(tsi,  tsl)
bool  crossDn     = ta.crossunder(tsi, tsl)
bool  cross       = ta.cross(tsi,      tsl)
bool  tsiXUp      = ta.crossover(tsi,  oBotInput) 
bool  tsiXDn      = ta.crossunder(tsi, oSoldInput)

bool  osCrossUp   = crossUp and tsi <= oSoldInput
bool  obCrossDn   = crossDn and tsi >= oBotInput

bool  isLight     = isLightTheme()
bool  isClassic   = themeInput == TI1
bool  isBeachy    = themeInput == TI3

bool showObFill   = tsl > oBotInput  and not isClassic 
bool showOsFill   = tsl < oSoldInput and not isClassic 

// Detect switch from curlUp (tsiBullInput) to tsiMidBelowInput
// Previous bar: curlUp[1] was true (using tsiBullInput - green)
// Current bar: curlUp is false and trendIsBull is false (using tsiMidBelowInput - red)
bool prevWasCurlUp = isClassic and curlUp[1]
bool currIsMidBelow = isClassic and not curlUp and not trendIsBull
bool colorSwitchCurlUpToMidBelow = prevWasCurlUp and currIsMidBelow
bool newColorSwitchCurlUpToMidBelow = colorSwitchCurlUpToMidBelow and not colorSwitchCurlUpToMidBelow[1]

// Detect switch from curlDn (tsiBearInput) to tsiMidAboveInput
// Previous bar: curlDn[1] was true (using tsiBearInput - red)
// Current bar: curlDn is false and trendIsBull is true (using tsiMidAboveInput - green)
bool prevWasCurlDn = isClassic and curlDn[1]
bool currIsMidAbove = isClassic and not curlDn and trendIsBull
bool colorSwitchCurlDnToMidAbove = prevWasCurlDn and currIsMidAbove
bool newColorSwitchCurlDnToMidAbove = colorSwitchCurlDnToMidAbove and not colorSwitchCurlDnToMidAbove[1]

// ===== Premarket TSI Range Calculation =====
// Check if we're in premarket session based on time (using specified timezone)
currentHour = hour(time, premarketTimezone)
currentMinute = minute(time, premarketTimezone)
currentTime = currentHour * 100 + currentMinute
premarketStartTime = premarketStartHour * 100 + premarketStartMinute
premarketEndTime = premarketEndHour * 100 + premarketEndMinute

// Check if current time is within premarket session
inPremarket = currentTime >= premarketStartTime and currentTime < premarketEndTime

// Track premarket high/low TSI values
var float premarketTsiHigh = na
var float premarketTsiLow = na

// Reset at start of new day (using specified timezone)
currentDay = dayofweek(time, premarketTimezone)
currentDayOfMonth = dayofmonth(time, premarketTimezone)
isNewDay = ta.change(currentDay) != 0 or ta.change(currentDayOfMonth) != 0

// Reset premarket values only on new day
if isNewDay
    premarketTsiHigh := na
    premarketTsiLow := na

// Update premarket high/low during premarket session
if inPremarket
    // Initialize both high and low with first value
    if na(premarketTsiHigh) and na(premarketTsiLow)
        premarketTsiHigh := tsi
        premarketTsiLow := tsi
    else
        // Update high if current value is higher
        if tsi > premarketTsiHigh
            premarketTsiHigh := tsi
        // Update low if current value is lower
        if tsi < premarketTsiLow
            premarketTsiLow := tsi

// Use the tracked values for the background range
// Values persist after premarket ends until next day
// Calculate range values - use the actual high/low values directly
premarketRangeUpper = na(premarketTsiHigh) ? na : premarketTsiHigh
premarketRangeLower = na(premarketTsiLow) ? na : premarketTsiLow
//#endregion



//#region ———————————————————— Colors


color tsiDnUpColor = isBeachy ? #1E90FF : #BA33FF
color tsiDnDnColor = isBeachy ? #FF6B6B : #8100FF
color tsiUpUpColor = isBeachy ? #00CED1 : #1FE0F3
color tslBullColor = isBeachy ? #00CED1 : #1FE0F3
color tslBearColor = isBeachy ? #1E90FF : #0088A3
color tsiBullColor = isBeachy ? #FFD700 : #33FF57
color tsiBearColor = isBeachy ? #FF6B6B : #00940D
color tsiUpDnColor = isBeachy ? #FFD700 : #FF21D4
color bullBgColor  = isBeachy ? #FFD700 : #80FFFF
color bearBgColor  = isBeachy ? #FF6B6B : #FF80FF
color obFillColor  = isBeachy ? #FFD700 : #33FF57
color osFillColor  = isBeachy ? #FF6B6B : #33FF57

color barBdColor  = trendIsBull  ? bullBgColor : bearBgColor
color barBgColor  = isLight ? trendIsBull ? bullBgColor : bearBgColor : chart.bg_color
int   barBdTransp = isLight ? 50 : 80
int   barBgTransp = isLight ? 90 : 0

color tsiTheme   = tsiIsBull ? tsiBullColor : tsiBearColor
color tslTheme   = tslIsBull ? tslBullColor : tslBearColor
color tsiColor   = isClassic ? curlUp ? tsiBullInput : curlDn ? tsiBearInput : trendIsBull ? tsiMidAboveInput : tsiMidBelowInput : tsiTheme
color tslColor   = isClassic ? tslIsBull ? tslBullInput : tslBearInput : tslTheme
color fillColor1 = isClassic ? trendIsBull ? fillBullInput : fillBearInput : na
color oBotColor  = isClassic ? oBotColInput  : color.new(obFillColor, 75)
color oSoldColor = isClassic ? oSoldColInput : color.new(osFillColor, 75)
color barColor   = overBought ? obBarInput : overSold ? osBarInput : blankBarsInput ? neBarInput : na
color fillColor2 = not isClassic ? trendIsBull ? 
	 tsiIsBull ? tsiUpUpColor : tsiUpDnColor : 
	 tsiIsBull ? tsiDnUpColor : tsiDnDnColor : color(na)
//#endregion



//#region ———————————————————— Display


// Horizontal area fills (drawn first for lowest z-index)
h3 = hline(strongBearValue, color = color(na), display = display.none, editable = false)
h4 = hline(bearAreaValue, color = color(na), display = display.none, editable = false)
h5 = hline(bullAreaValue, color = color(na), display = display.none, editable = false)
h6 = hline(strongBullValue, color = color(na), display = display.none, editable = false)
h7 = hline(-100, color = color(na), display = display.none, editable = false)
h8 = hline(100, color = color(na), display = display.none, editable = false)

fill(h7, h3, showStrongBear ? strongBearColor : na, title = "Strong Bear Area")
fill(h3, h4, showBearArea ? bearAreaColor : na, title = "Bear Area")
fill(h5, h6, showBullArea ? bullAreaColor : na, title = "Bull Area")
fill(h6, h8, showStrongBull ? strongBullColor : na, title = "Strong Bull Area")

// Premarket TSI range background
p_premarketUpper = plot(not na(premarketRangeUpper) and showPremarketRange ? premarketRangeUpper : na, "Premarket High", color = color.new(color.white, 100), display = display.none)
p_premarketLower = plot(not na(premarketRangeLower) and showPremarketRange ? premarketRangeLower : na, "Premarket Low", color = color.new(color.white, 100), display = display.none)
fill(p_premarketUpper, p_premarketLower, color = showPremarketRange and not na(premarketRangeUpper) and not na(premarketRangeLower) ? premarketRangeColor : na, title = "Premarket TSI Range")

h1 = hline(isClassic ? na : oBotInput,  color = color(na), display = display.none, editable = false)
h2 = hline(isClassic ? na : oSoldInput, color = color(na), display = display.none, editable = false)

fill(h2, h1, oBotInput,   5, color.new(obFillColor, 80), color(na), fillgaps = true)
fill(h2, h1, -5, oSoldInput, color(na), color.new(osFillColor, 80), fillgaps = true)

plotcandle(barLo, barLo, barHi, barHi,
     color       = isClassic ? na : color.new(barBgColor, barBgTransp),
     bordercolor = isClassic ? na : color.new(barBdColor, barBdTransp),
     wickcolor   = color(na),
     display     = display.pane
 )

p1 = plot(tsi, "TSI Value Line",  tsiColor)
p2 = plot(tsl, "TSI Signal Line", tslColor)
p3 = plot(showLinesInput ? oBotInput  : na, "Overbought", oBotColor)
p4 = plot(showLinesInput ? oSoldInput : na, "Oversold",   oSoldColor)
p5 = plot(showMidInput   ? 0          : na, "Zero",       zeroColInput)
p8 = plot(showNoShortInput ? noShortInput : na, "No Short Line", noShortColInput)
p9 = plot(showOnLongInput  ? onLongInput  : na, "On Long Line",  onLongColInput)
p6 = plot(oBotInput , display = display.none, editable = false)
p7 = plot(oSoldInput, display = display.none, editable = false)

// Plot for Trend Continue Bear: Green to Red below signal line
// Only plots when switching from curlUp (green) to midBelow (red) while below signal
float trendContinueBearPlot = na
if showTrendContinueBear and newColorSwitchCurlUpToMidBelow
    trendContinueBearPlot := tsi + switchOffset

p11 = plot(trendContinueBearPlot, "Trend Continue Bear", trendContinueBearColor, linewidth = switchSize, style = plot.style_cross)

// Plot for Trend Continue Bull: Red to Green above signal line
// Only plots when switching from curlDn (red) to midAbove (green) while above signal
float trendContinueBullPlot = na
if showTrendContinueBull and newColorSwitchCurlDnToMidAbove
    trendContinueBullPlot := tsi + switchOffset

p12 = plot(trendContinueBullPlot, "Trend Continue Bull", trendContinueBullColor, linewidth = switchSize, style = plot.style_cross)

displayFill = isClassic ? display.none : display.all
int obValue = showObFill ? oBotInput  : na
int osValue = showOsFill ? oSoldInput : na
fill(p2, p6, oBotInput + 50, obValue,  color.new(bearBgColor, 30), color.new(bearBgColor, 90), fillgaps = true)
fill(p2, p7, osValue, oSoldInput - 50, color.new(bullBgColor, 90), color.new(bullBgColor, 30), fillgaps = true)
fill(p1, p2, fillColor1, fillgaps = true)
fill(p1, p2, tsi, tsl, fillColor2, color.new(fillColor2, 70), fillgaps = true, display = displayFill)
barcolor(showCurlInput ? barColor : na)

bgcolor(osCrossUp and showHiLoInput ? isClassic ? color.new(oBotColInput,  80) : color.new(bullBgColor, 80) : na)
bgcolor(obCrossDn and showHiLoInput ? isClassic ? color.new(oSoldColInput, 80) : color.new(bearBgColor, 80) : na)
//#endregion



//#region ———————————————————— Alerts

// Determine alert frequency
alertFreq = webhookFrequency == "Once Per Bar Close" ? alert.freq_once_per_bar_close : 
           webhookFrequency == "Once Per Bar" ? alert.freq_once_per_bar : 
           alert.freq_all

// Webhook JSON payload
if enableWebhook and barstate.isconfirmed
    jsonMsg = '{' +
      '"symbol": "' + syminfo.ticker + '",' +
      '"timeframe": "' + timeframe.period + '",' +
      '"time": "' + str.tostring(time) + '",' +
      '"price": ' + str.tostring(close) + ',' +
      '"bjTsi": ' + str.tostring(tsi, "#.###") + ',' +
      '"bjTsl": ' + str.tostring(tsl, "#.###") + ',' +
      '"bjTsiIsBull": ' + str.tostring(tsiIsBull) + ',' +
      '"bjTslIsBull": ' + str.tostring(tslIsBull) + ',' +
      '"bjPremarketTsiHigh": ' + (na(premarketTsiHigh) ? "null" : str.tostring(premarketTsiHigh, "#.###")) + ',' +
      '"bjPremarketTsiLow": ' + (na(premarketTsiLow) ? "null" : str.tostring(premarketTsiLow, "#.###")) + '}'
    alert(jsonMsg, freq = alertFreq)

// Simple alerts (for backward compatibility)
if osCrossUp
    alert("Turn Bullish", alert.freq_once_per_bar)

if obCrossDn
    alert("Turn Bearish", alert.freq_once_per_bar)

if crossUp
    alert("TSI crossover (TSI crossed above Signal)", alert.freq_once_per_bar)

if crossDn
    alert("TSI crossunder (TSI crossed below Signal)", alert.freq_once_per_bar)
//#endregion
